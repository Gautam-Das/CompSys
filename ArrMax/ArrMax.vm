// Function to find the largest element in an array
// Function signature: ArrMax.arrMax(a, b)
// a: starting address of the array (argument 0)
// b: length of the array (argument 1)

function ArrMax.arrMax 2
    // Check if the array length is less than or equal to 0
    push argument 1
    push constant 1 
    lt   
    if-goto ArrMax.arrMax$il

    // Initialize counter to 0
    push constant 0
    pop temp 0

    // Set pointer to the start
    push argument 0
    pop pointer 1

    // first element is current max
    push that 0
    pop temp 1

    label ArrMax.arrMax$loop
        // Increment counter
        push temp 0
        push constant 1
        add
        pop temp 0

        // if counter == n
        push argument 1
        push temp 0
        eq
        if-goto ArrMax.arrMax$loopEnd

        // increase that
        push argument 0
        push temp 0
        add
        pop pointer 1

        // get current element
        push that 0
        pop temp 2

        // check if current element is greater than current max
        push temp 2
        push temp 1
        gt
        if-goto ArrMax.arrMax$newMax
        goto ArrMax.arrMax$loop

    label ArrMax.arrMax$newMax
        // change max
        push temp 2
        pop temp 1
        goto ArrMax.arrMax$loop

    label ArrMax.arrMax$loopEnd
        // return
        push temp 1
        return

    label ArrMax.arrMax$il
        push constant 0
        return
